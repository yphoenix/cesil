* CALCULATE PI, BASED ON LEIBNIZ FORMULA PI = 4/1 - 4/3 + 4/5 - 4/7...
* BECAUSE CESIL DOES NOT DEAL WITH FRACTIONS, CALCULATE 100000000*PI FIRST

* (c) SIMON ROBINSON, 2011

* SOME OUTPUT LINES HAVE BEEN COMMENTED OUT TO MAKE THE PROGRAM RUN FASTER,
* THEY CAN BE RESTORED TO
	IN
	ADD 1
	STORE MAXCOUNT

*MAXCOUNT IS THE NUMBER OF ITERATIONS TO PERFORM + 1

	LOAD 1
	STORE COUNT
	STORE PLUSMIN
	LOAD 0
	STORE PI

LOOP	LOAD PI
	STORE LASTPI
	LOAD COUNT
*	PRINT "Iteration "
*	OUT
*	PRINT ": PI = "

* NEXT STAGE IS CALCULATE 400000000/(2*COUNT-1)
* TO AVOID ROUND-DOWN ERRORS, THIS BECOMES INT((800000000+2*COUNT-1)/(4*COUNT-2))

	MULTIPLY 4
	SUBTRACT 2
	STORE DEN
	LOAD COUNT
	MULTIPLY 2
	ADD 800000000
	SUBTRACT 1
	DIVIDE DEN

* THE +/- OF THE SERIES CHANGES ALTERNATELY

	MULTIPLY PLUSMIN
	ADD PI
	STORE PI
*	OUT
*	LINE
	LOAD PLUSMIN
	MULTIPLY -1
	STORE PLUSMIN
	LOAD COUNT
	ADD 1
	STORE COUNT
	SUBTRACT MAXCOUNT
	JINEG LOOP

* TAKE AVERAGE OF LAST TWO CALCULATED VALUES FOR FINAL OUTPUT

	LOAD PI
	ADD LASTPI
	DIVIDE 2
	STORE PI

* TO PRINT DECIMAL VALUE, WORK OUT INTEGER VALUE, PRINT THAT AND ".", THEN REST OF FIGURE.

	PRINT "PI = "
	DIVIDE 100000000
	OUT
	PRINT "."
	MULTIPLY 100000000
	SUBTRACT PI
	MULTIPLY -1
	OUT
	HALT

* DATA
* THE HIGHER THIS NUMBER IS, THE MORE ACCURATE THE RESULT BUT THE LONGER IT WILL TAKE TO RUN

	%
	1000000
